plugins {
	id 'org.springframework.boot' version '2.1.8.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
}

group = 'com.wheelsfree'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

test {
	useJUnitPlatform()
}

task createDB(type: Exec) {
	commandLine './db/create.sh'
}

task initDB(type: Exec) {
	workingDir 'db/migrations'
	commandLine './mybatis/bin/migrate', 'up'
	dependsOn createDB
}

sourceSets {
	integrationTest {
		compileClasspath += sourceSets.main.output + configurations.testCompile
		runtimeClasspath += output + compileClasspath + configurations.testRuntime
	}
}

task integrationTest(type: Test) {
	useJUnitPlatform()
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
	mustRunAfter test
	dependsOn initDB
}

check.dependsOn integrationTest

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.postgresql:postgresql:42.1.4'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
	implementation 'org.mybatis:mybatis:3.4.5'
	implementation 'org.mybatis:mybatis-spring:2.0.3'
	implementation 'org.springframework:spring-jdbc:5.0.2.RELEASE'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mockito:mockito-core:3.0.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
	testImplementation 'org.mybatis:mybatis-migrations:3.3.6'
	testImplementation "org.mybatis.spring.boot:mybatis-spring-boot-starter-test:2.1.1"

	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}
